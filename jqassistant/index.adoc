
== Structural Rules And Reports

The following constraints are verified:

- <<dep:repoToServiceOrViewOrDTO>>
- <<dep:serviceToView>>
- <<dep:viewToDomainOrRepository>>
- <<dep:entitiesToOtherLayer>>
- <<dep:dtoToOtherLayer>>
- Package cycles (defined by jqassistant-rule)

The following Reports are created:

- <<structure:LayerDependencies.graphml>>


[[default]]
[role=group,includesConstraints="dependency:PackageCycles,dep:repoToServiceOrViewOrDTO,dep:serviceToView,dep:viewToDomainOrRepository,dep:entitiesToOtherLayer,dep:dtoToOtherLayer"]


=== Constraints

[[dep:repoToServiceOrViewOrDTO]]
[source,cypher,role="constraint",depends="layer:Repository,layer:Service,layer:View"]
.No Dependency between Repositories and Services or Views or DTO
----
 MATCH (t:Class) -[:DEPENDS_ON]-> (otherT:Class)
 WHERE t:Repository AND (otherT:Service OR otherT:View OR otherT:Dto)
 RETURN t as InvalidDependency
----

[[dep:serviceToView]]
[source,cypher,role="constraint",depends="layer:Service,layer:View"]
.No Dependency between Services and View
----
 MATCH (t:Class) -[:DEPENDS_ON]-> (otherT:Class)
 WHERE t:Service AND otherT:View
 RETURN t as InvalidDependency
----


[[dep:viewToDomainOrRepository]]
[source,cypher,role="constraint",depends="layer:View,layer:Repository,layer:Domain"]
.No Dependency between View and Repository or Entities
----
 MATCH (t:View) -[:DEPENDS_ON]-> (otherT:Class)
 WHERE otherT:Repository OR otherT:Domain
 RETURN t as InvalidDependency
----


[[dep:entitiesToOtherLayer]]
[source,cypher,role="constraint",depends="layer:View,layer:Service,layer:Repository,layer:Domain,layer:DTO"]
.Entities should not have any dependency
----
MATCH (t:Domain) -[:DEPENDS_ON]-> (otherT:Class)
WHERE t <> otherT
RETURN t as InvalidDependency
----

[[dep:dtoToOtherLayer]]
[source,cypher,role="constraint",depends="layer:View,layer:Service,layer:Repository,layer:Domain,layer:DTO"]
.DTOÂ´s should not have any dependency
----
 MATCH (t:Dto) -[:DEPENDS_ON]-> (otherT:Class)
 where t<>otherT
 RETURN t as InvalidDependency
----

==== Concepts

[[layer:Repository]]
[source,cypher,role="concept"]
.Match all repositories
----
  MATCH
        (t:Type)
  WHERE
        t.fqn =~ ".*repo.*"
  SET
        t:Repository return t
----


[[layer:Service]]
[source,cypher,role="concept"]
.Match all services
----
  MATCH
        (t:Type)
  WHERE
        t.fqn =~ ".*service.*"
  SET
        t:Service return t
----

[[layer:View]]
[source,cypher,role="concept"]
.Match all views
----
  MATCH
        (t:Type)
  WHERE
        t.fqn =~ ".*view.*"
  SET
        t:View return t
----

[[layer:Domain]]
[source,cypher,role="concept"]
.Match all views
----
  MATCH
        (t:Type)
  WHERE
        t.fqn =~ ".*model.domain.*"
  SET
        t:Domain return t
----


[[layer:DTO]]
[source,cypher,role="concept"]
.Match all views
----
  MATCH
        (t:Type)
  WHERE
        t.fqn =~ ".*model.dto.*"
  SET
        t:Dto return t
----

[[structure:LayerDependencies]]
[source,cypher,role=concept,requiresConcepts="layer:Repository,layer:Service,layer:View,layer:Domain,layer:DTO"]
.Creates a USES relation between application layer items, i.e. controller, service and repository implementations.
----
WITH
  [
    "View",
    "Service",
    "Repository"
    "Domain",
    "DTO",
  ] as selectedRoles
MATCH
  (type:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(:Type:Interface)<-[:IMPLEMENTS|EXTENDS*]-(otherType:Type)
WHERE
  type <> otherType
  and any
    (typeRole in labels(type) WHERE typeRole in selectedRoles)
  and any
    (otherTypeRole in labels(otherType) WHERE otherTypeRole in selectedRoles)
MERGE
  (type)-[:USES]->(otherType)
RETURN
  type as Dependent, collect(distinct otherType.fqn) as Dependencies
----
